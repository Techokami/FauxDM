/*
================================================================================
 FauxDM ZScript Code
 
 Author: The Kinsie
 
 Purpose: Rocket Launcher weapon
 
 Comment: Basically ripped straight from Null. Recycling, yo.

================================================================================
*/

class FDM_RocketLauncher : FDM_BaseWeapon replaces RocketLauncher
{
	Default
	{
		Weapon.SelectionOrder 2500;
		Weapon.AmmoUse 1;
		Weapon.AmmoGive 2;
		Weapon.AmmoType "RocketAmmo";
		+WEAPON.NOAUTOFIRE
		Inventory.PickupMessage "$GOTLAUNCHER";
		Tag "$TAG_ROCKETLAUNCHER";
		//$Title "Rocket Launcher"
	}
	States
	{
	Ready:
		RLAU A 1 A_WeaponReady;
		Loop;
	Deselect:
		RLAU A 1 A_DeselectionShit();
		"####" A 1 A_WeaponOffset(0,33,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,35,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,39,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,44,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,49,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,55,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,60,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,70,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,80,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,90,WOF_INTERPOLATE);
	FinishDeselect:
		"####" A 0 A_Lower;
		Loop;
	Select:
		RLAU A 1 A_WeaponOffset(0,90,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,80,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,70,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,60,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,55,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,49,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,44,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,39,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,35,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,33,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,32,WOF_INTERPOLATE);
		"####" A 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE);
		goto Ready;
	Fire:
		RLAU B 0 BRIGHT
		{
			A_GunFlash();
			A_ZoomFactor(0.95,ZOOM_NOSCALETURNING);
		}
		RLAU C 2 BRIGHT
		{
			A_PlaySound("weapons/rocket/fire");
			A_FireProjectile("FDM_PlayerRocket");
			A_ZoomFactor(0.9,ZOOM_NOSCALETURNING);
			A_QuadNoise();
		}
		RLAU D 2 BRIGHT A_ZoomFactor(0.95,ZOOM_NOSCALETURNING);
		RLAU E 2 BRIGHT A_ZoomFactor(0.96,ZOOM_NOSCALETURNING);
		RLAU F 2 BRIGHT A_ZoomFactor(0.97,ZOOM_NOSCALETURNING);
		RLAU G 2 BRIGHT A_ZoomFactor(0.98,ZOOM_NOSCALETURNING);
		RLAU H 2 A_ZoomFactor(0.99,ZOOM_NOSCALETURNING);
		RLAU A 2 A_ZoomFactor(1.0,ZOOM_NOSCALETURNING);
		RLAU AAAAAA 2;
		RLAU A 0 A_ReFire;
		Goto Ready;
	Flash:
		TNT1 A 3 Bright A_Light1;
		TNT1 A 4 Bright;
		TNT1 AA 4 Bright A_Light2;
		Goto LightDone;
	Spawn:
		RLAU Z -1;
		Stop;
	}
}

class FDM_Rocket : Actor replaces Rocket
{
	Default
	{
		Radius 11;
		Height 8;
		Speed 20;
		Damage 20;
		Projectile;
		+RANDOMIZE
		+ROCKETTRAIL
		Obituary "$OB_MPROCKET";
	}
	States
	{
	Spawn:
		MISL A 1 Bright A_PlaySound("weapons/rocket/loop", CHAN_BODY, 1.0, 1);
		Loop;
	Death:
		MISL B 0
		{
			A_Explode();
			A_SpawnItem("FDM_RocketExplosionVisualOnly");
		}
		Stop;
	}
}

class FDM_PlayerRocket : FDM_Rocket
{
	Default
	{
		Speed 25;
		damagetype "Rocket";
	}
}