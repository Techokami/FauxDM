/*
================================================================================
 FauxDM ZScript Code
 
 Author: Boondorl, The Kinsie
 
 Purpose: Flamethrower weapon code
 
 Comment: Based off of Boondorl's Generic Flamethrower library
 https://forum.zdoom.org/viewtopic.php?f=37&t=65883

================================================================================
*/

class FDM_Flamethrower : FDM_BaseWeapon replaces Chainsaw
{
	Default
	{
		Weapon.SlotNumber 5;
		Weapon.SelectionOrder 2500;
		Weapon.AmmoUse 1;
		Weapon.AmmoGive 50;
		Weapon.AmmoType "FDM_Fuel";
		Inventory.PickupMessage "$GOTFLAMER";
		Tag "$TAGFLAMER";
		scale 0.75;
	}
	
	States
	{
		Ready:
			THRO A 1 A_WeaponReady;
			Loop;
		Deselect:
			THRO A 1 A_DeselectionShit();
			"####" A 1 A_WeaponOffset(0,33,WOF_INTERPOLATE);
			"####" A 1 A_WeaponOffset(0,35,WOF_INTERPOLATE);
			"####" A 1 A_WeaponOffset(0,39,WOF_INTERPOLATE);
			"####" A 1 A_WeaponOffset(0,44,WOF_INTERPOLATE);
			"####" A 1 A_WeaponOffset(0,49,WOF_INTERPOLATE);
			"####" A 1 A_WeaponOffset(0,55,WOF_INTERPOLATE);
			"####" A 1 A_WeaponOffset(0,60,WOF_INTERPOLATE);
			"####" A 1 A_WeaponOffset(0,70,WOF_INTERPOLATE);
			"####" A 1 A_WeaponOffset(0,80,WOF_INTERPOLATE);
			"####" A 1 A_WeaponOffset(0,90,WOF_INTERPOLATE);
		FinishDeselect:
			"####" A 0 A_Lower;
			Loop;
		Select:
			THRO A 1 A_WeaponOffset(0,90,WOF_INTERPOLATE);
			"####" A 1 A_WeaponOffset(0,80,WOF_INTERPOLATE);
			"####" A 1 A_WeaponOffset(0,70,WOF_INTERPOLATE);
			"####" A 1 A_WeaponOffset(0,60,WOF_INTERPOLATE);
			"####" A 1 A_WeaponOffset(0,55,WOF_INTERPOLATE);
			"####" A 1 A_WeaponOffset(0,49,WOF_INTERPOLATE);
			"####" A 1 A_WeaponOffset(0,44,WOF_INTERPOLATE);
			"####" A 1 A_WeaponOffset(0,39,WOF_INTERPOLATE);
			"####" A 1 A_WeaponOffset(0,35,WOF_INTERPOLATE);
			"####" A 1 A_WeaponOffset(0,33,WOF_INTERPOLATE);
			"####" A 1 A_WeaponOffset(0,32,WOF_INTERPOLATE);
			"####" A 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE);
			goto Ready;
		Fire:
			THRO B 2 BRIGHT 
			{
				A_PlaySound("weapons/flamethrower/fire", CHAN_WEAPON);
				A_FireProjectile("FDM_FlamethrowerProjectile");
				A_GunFlash();
				A_QuadNoise();
			}
			THRO C 2 BRIGHT;
			TNT1 A 0 A_ReFire;
			Goto StopFiring;
		Hold:
			THRO D 2 BRIGHT
			{
				A_PlaySound("weapons/flamethrower/fire", CHAN_WEAPON);
				A_FireProjectile("FDM_FlamethrowerProjectile");
				A_GunFlash();
				A_QuadNoise();
			}
			THRO D 2 BRIGHT;
			TNT1 A 0 A_ReFire;
			Goto StopFiring;
		StopFiring:
			THRO EEFG 3;
			Goto Ready;
		Flash:
			TNT1 A 2 Bright A_Light1;
			Goto LightDone;
		Spawn:
			THRO Z -1;
			Stop;
	}
}

class FDM_FlamethrowerProjectile : CustomProj
{
	Default
	{
		Radius 20;
		Height 40;
		CustomProj.RadiusScale 0.1;
		CustomProj.HeightScale 0.1;
		CustomProj.RadiusScaleFactor 0.03;
		CustomProj.HeightScaleFactor 0.03;
		
		CustomProj.CustomDamage 10;
		DamageType "Flamer";
		CustomProj.SplashDamage 32;
		CustomProj.SplashRadiusScale 2;
		
		CustomProj.DOTItem "FDM_BurnDOT";
		CustomProj.AOEActor "FDM_FireAOE";
		
		Scale 0.4;
		CustomProj.XScaleFactor 0.03;
		CustomProj.YScaleFactor 0.03;
		
		Speed 24;
		RenderStyle "Add";
		//Alpha 0.75;
		Obituary "$OB_FLAMETHROWER";
		
		+FORCEXYBILLBOARD
		
		-NOGRAVITY
	}
	
	States
	{
		Spawn:
			FTHR A 0;
			FTHR A 1 Bright ThrustThingZ(0,16,0,1);
		Move:
			FTHR ABCDABCDABCDABCD 2 Bright;
			Stop;
			
		Death:
			TNT1 A 0 
			{
				A_SpawnAreaOfEffect();
				A_PlaySound("weapons/flamethrower/hit");
			}
			MISL C 3 Bright A_Splash(XF_NOSPLASH);
			MISL D 2 Bright;
			Stop;
	}
	
	override void Tick()
	{
		super.Tick();
		
		if (isFrozen() || level.isFrozen())
			return;
		
		alpha -= min(0.02, alpha);
		
		if (waterlevel > 0)
		{
			A_PlaySound("weapons/flamethrower/sizzle");
			A_Remove(AAPTR_DEFAULT,RMVF_MISSILES);
		}
		
	}
}

class FDM_BurnDOT : CustomDOT
{
	Default
	{
		Damage 5;
		DamageType "Flamer";
		
		CustomDOT.NumberOfTics 3;
		CustomDOT.Duration 3;
		CustomDOT.MaxTotalDOTs 7;
		CustomDOT.EffectSpawnRate 0.01;
		CustomDOT.StackType "Stackable";
		CustomDOT.DamageApplication "Delayed";
		CustomDOT.EffectActor "FDM_FlameEffect";
		
		Obituary "$OB_FLAMETHROWER_DOT";
	}
}

class FDM_FlameEffect : DOTEffect
{
	Default
	{
		DOTEffect.NoWarp true;
		
		RenderStyle "Add";
		Alpha 0.75;
		Scale 0.5;
		+DONTSPLASH
	}
	
	double crackvolume;
	
	States
	{
		Spawn:
			FTRA A 0;
			FTRA A 0 { crackvolume = 0.25; }
			FTRA ABCDEFGHIJK 1 Bright;
			Stop;
	}
	
	override void Tick()
	{
		super.Tick();
		
		crackvolume - 0.1;
		
		A_PlaySound("weapons/flamethrower/crak",CHAN_BODY,crackvolume,0,ATTN_NORM,false,frandom(0.5,1));
	}
}

class FDM_FireAOE : CustomAOE
{
	static const String noSpawn[] =
	{
		"FWATER1", "FWATER2", "FWATER3", "FWATER4",
		"BLOOD1", "BLOOD2", "BLOOD3", "sw0780"
	};
	
	Default
	{
		Radius 16;
		Height 24;
		Damage 5;
		DamageType "Flamer";
		
		CustomAOE.CoolDown 0.25;
		CustomAOE.DOTItem "FDM_BurnDOT";
		CustomAOE.EffectSpawnRate 0.1;
		CustomAOE.EffectActor "FDM_GroundFireFlame";
	}
	
	States
	{
		Spawn:
			TNT1 A 70;
			Stop;
	}
	
	override void BeginPlay()
	{
		super.BeginPlay();
		
		TextureID offLimits;
		for (int i = 0; i < noSpawn.Size(); ++i)
		{
			offLimits = TexMan.CheckForTexture(noSpawn[i], TexMan.Type_Flat);
			if (floorpic == offLimits)
			{
				Destroy();
				return;
			}
		}
	}
		
	override void Tick()
	{
		super.Tick();
		
		if (waterlevel > 0)
		{
			A_PlaySound("weapons/flamethrower/sizzle");
			A_Remove(AAPTR_DEFAULT,RMVF_EVERYTHING);
		}
	}
	
}

class FDM_GroundFireFlame : Actor
{
	Default
	{
		RenderStyle "Add";
		Alpha 0.9;
		Scale 0.5;
		
		+NOINTERACTION
	}
	
	States
	{
		Spawn:
			FFLA ABCDEFGHIJKLMN 2 Bright;
			Stop;
	}
	
	override void Tick()
	{
		super.Tick();
		
		if (isFrozen() || level.isFrozen())
			return;
		
		scale.x -= min(0.02, scale.x);
		scale.y -= min(0.02, scale.y);
		
		A_PlaySound("weapons/flamethrower/crak",CHAN_BODY,(scale.x / 2),0,ATTN_NORM,false,frandom(0.1,0.5));
		
		if (waterlevel > 0)
		{
			A_PlaySound("weapons/flamethrower/sizzle");
			A_Remove(AAPTR_DEFAULT,RMVF_EVERYTHING);
		}
	}
}