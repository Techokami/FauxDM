/*
================================================================================
 FauxDM ZScript Code
 
 Author: The Kinsie
 
 Purpose: Shared weapon code
 
 Comment:

================================================================================
*/

class FDM_BaseWeapon : DoomWeapon
{
	States
	{
	Ready:
	Select:
	Deselect:
	Fire:
		TNT1 A 1;
		Loop;
	Deadman:
		TNT1 A 1
		{
			A_StopSound(CHAN_6);
			A_StopSound(CHAN_7);
		}
		Loop;
	}
	
	// Don't play the weapon switch sound when dying
	// Thanks to Phantombeta for fixing this
	action State A_DeselectionShit()
	{
		if (health <= 0)
			return ResolveState("Deadman");
		else
			A_PlaySound("weapons/swap");

		return ResolveState(null);
	}
	
	action void A_QuadNoise()
	{
		if (CountInv("PowerDamage") > 0)
		{
			A_PlaySound("weapons/quad", CHAN_5);
		}
	}
}


class FDM_BulletPuff : Actor replaces BulletPuff
{
	Default
	{
		+NOBLOCKMAP
		+NOGRAVITY
		+ALLOWPARTICLES
		+RANDOMIZE
		RenderStyle "Translucent";
		Alpha 0.5;
		VSpeed 1;
		Mass 5;
	}
	States
	{
	Spawn:
		PUFF A 0;
		PUFF A 4 Bright
		{
			int rico = random(1,4);
			
			if (rico == 4)
			{
				A_PlaySound("weapons/ric");
				
				for (int i; i < 3; i++)
				{
					A_SpawnParticle("ffc400",SPF_RELATIVE|SPF_FULLBRIGHT,random(0,56),random(3,8),0,0,-8,0,frandom(2.0,-2.0),frandom(2.0,-2.0),frandom(1,8),0,0,-0.4);
					A_SpawnParticle("ff8a00",SPF_RELATIVE|SPF_FULLBRIGHT,random(0,56),random(3,8),0,0,-8,0,frandom(2.0,-2.0),frandom(2.0,-2.0),frandom(1,8),0,0,-0.4);
				}
			}
		}
		PUFF B 4;
	Melee:
		PUFF CD 4;
		Stop;
	}
}