/*
================================================================================
 FauxDM ZScript Code
 
 Author: The Kinsie
 
 Purpose: Grenade Launcher weapon
 
 Comment:

================================================================================
*/

class FDM_GrenadeLauncher : FDM_BaseWeapon
{
	Default
	{
		Weapon.SelectionOrder 2500;
		Weapon.AmmoUse 1;
		Weapon.AmmoGive 2;
		Weapon.AmmoType "RocketAmmo";
		+WEAPON.NOAUTOFIRE
		Inventory.PickupMessage "$GOTGRENL";
		Tag "$TAGGRENL";
		//$Category "Weapons"
		//$Title "Grenade Launcher"
		//$NotAngled
		//$Color 14
	}
	States
	{
	Ready:
		GLAU A 1 A_WeaponReady;
		Loop;
	Deselect:
		GLAU A 1 A_DeselectionShit();
		"####" A 1 A_WeaponOffset(0,33,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,35,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,39,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,44,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,49,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,55,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,60,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,70,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,80,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,90,WOF_INTERPOLATE);
	FinishDeselect:
		"####" A 0 A_Lower;
		Loop;
	Select:
		GLAU A 1 A_WeaponOffset(0,90,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,80,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,70,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,60,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,55,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,49,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,44,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,39,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,35,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,33,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,32,WOF_INTERPOLATE);
		"####" A 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE);
		goto Ready;
	Fire:
		GLAU B 0 BRIGHT
		{
			A_GunFlash();
			A_ZoomFactor(0.95,ZOOM_NOSCALETURNING);
		}
		GLAU C 2 BRIGHT
		{
			A_PlaySound("weapons/grenade/fire");
			A_FireProjectile("FDM_Grenade");
			A_ZoomFactor(0.9,ZOOM_NOSCALETURNING);
		}
		GLAU D 2 BRIGHT A_ZoomFactor(0.95,ZOOM_NOSCALETURNING);
		GLAU E 2 BRIGHT A_ZoomFactor(0.96,ZOOM_NOSCALETURNING);
		GLAU F 2 BRIGHT A_ZoomFactor(0.97,ZOOM_NOSCALETURNING);
		GLAU G 2 BRIGHT A_ZoomFactor(0.98,ZOOM_NOSCALETURNING);
		GLAU A 2 A_ZoomFactor(0.99,ZOOM_NOSCALETURNING);
		GLAU A 2 A_ZoomFactor(1.0,ZOOM_NOSCALETURNING);
		GLAU AAAAAA 2;
		GLAU A 0 A_ReFire;
		Goto Ready;
	Flash:
		TNT1 A 3 Bright A_Light1;
		TNT1 A 4 Bright;
		TNT1 AA 4 Bright A_Light2;
		Goto LightDone;
	Spawn:
		GLAU Z -1;
		Stop;
	}
}

class FDM_Grenade : Actor
{
	Default
	{
		Radius 8;
		Height 8;
		Speed 20;
		Damage 20;
		Projectile;
		-NOGRAVITY
		+RANDOMIZE
		+GRENADETRAIL
		BounceType "Doom";
		Gravity 0.75;
		BounceSound "weapons/grenade/bounce";
		Obituary "$OB_GRENADE";
	}
	States
	{
	Spawn:
		GREN A 0;
		GREN A 1 Bright ThrustThingZ(0,16,0,1);
	Move:
		GREN A 1 Bright;
		Loop;
	Death:
		MISL B 0
		{
			A_Explode();
			A_SpawnItem("FDM_RocketExplosionVisualOnly");
		}
		Stop;
	}
}