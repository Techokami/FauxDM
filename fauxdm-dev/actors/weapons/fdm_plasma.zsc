/*
================================================================================
 FauxDM ZScript Code
 
 Author: The Kinsie
 
 Purpose: Plasma Rifle weapon
 
 Comment: Basically pulled wholesale from NULL

================================================================================
*/

class FDM_PlasmaRifle : FDM_BaseWeapon replaces PlasmaRifle
{
	Default
	{
		Weapon.SelectionOrder 100;
		Weapon.AmmoUse 1;
		Weapon.AmmoGive 40;
		Weapon.AmmoType "Cell";
		Inventory.PickupMessage "$GOTPLASMA";
		Tag "$TAG_PLASMARIFLE";
		//$Title "Plasma Gun"
	}
	States
	{
	Ready:
		BEEM A 1 A_WeaponReady;
		Loop;
	Deselect:
		BEEM A 1 A_DeselectionShit();
		"####" A 1 A_WeaponOffset(0,33,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,35,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,39,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,44,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,49,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,55,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,60,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,70,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,80,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,90,WOF_INTERPOLATE);
	FinishDeselect:
		"####" A 0 A_Lower;
		Loop;
	Select:
		BEEM A 1 A_WeaponOffset(0,90,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,80,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,70,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,60,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,55,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,49,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,44,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,39,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,35,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,33,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,32,WOF_INTERPOLATE);
		"####" A 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE);
		goto Ready;
	Fire:
		BEEM B 1 
		{
			A_GunFlash();
			A_FireProjectile("FDM_PlasmaBall");
			A_PlaySound ("weapons/beem/fire", CHAN_WEAPON);
			A_GunFlash();
			A_WeaponOffset(0, 42);
			A_ZoomFactor(0.994,ZOOM_INSTANT|ZOOM_NOSCALETURNING);
		}
		BEEM C 1 BRIGHT { A_WeaponOffset(0, 40, WOF_INTERPOLATE); A_ZoomFactor(0.998,ZOOM_NOSCALETURNING); }
		BEEM D 1 BRIGHT { A_WeaponOffset(0, 38, WOF_INTERPOLATE); A_ZoomFactor(0.999,ZOOM_NOSCALETURNING); }
		BEEM A 1 { A_WeaponOffset(0, 34, WOF_INTERPOLATE); A_ZoomFactor(1.0,ZOOM_NOSCALETURNING); }
		BEEM A 0 A_WeaponOffset(0, 32, WOF_INTERPOLATE);
		BEEM A 0 A_ReFire;
		Goto Ready;
	Flash:
		TNT1 A 4 Bright A_Light1;
		Goto LightDone;
		TNT1 A 4 Bright A_Light1;
		Goto LightDone;
	Spawn:
		BEEM Z -1;
		Stop;
	}
}

class FDM_PlasmaBall : Actor
{
	Default
	{
		Radius 13;
		Height 8;
		Speed 25;
		Damage 5;
		Projectile;
		+RANDOMIZE
		RenderStyle "Add";
		Alpha 0.75;
		//SeeSound "weapons/plasmaf";
		DeathSound "weapons/beem/hit";
		Obituary "$OB_MPPLASMARIFLE";
		decal "StarSoldierPlasmaScorch";
	}
	States
	{
	Spawn:
		SSPB AB 6 Bright;
		Loop;
	Death:
		SSPB C 0 Bright
		{
			A_Explode ((16), 32);
			
			for (int i; i < 6; i++)
			{
				A_SpawnParticle("LightBlue",SPF_RELATIVE,random(0,56),random(3,8),0,0,-4,(height / 2),frandom(2.0,-2.0),frandom(2.0,-2.0),frandom(1,8),0,0,-0.4);
				A_SpawnParticle("White",SPF_RELATIVE,random(16,32),random(3,8),0,-4,0,(height / 2),frandom(2.0,-2.0),frandom(2.0,-2.0),frandom(1,8),0,0,-0.4);
			}
		}
		SSPB CDEFG 2 Bright;
		Stop;
	}
}