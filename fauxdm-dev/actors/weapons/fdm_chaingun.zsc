/*
================================================================================
 FauxDM ZScript Code
 
 Author: The Kinsie
 
 Purpose: Machinegun weapon
 
 Comment: 

================================================================================
*/

class FDM_Chaingun : FDM_BaseWeapon replaces Chaingun
{
	Default
	{
		Weapon.SelectionOrder 700;
		Weapon.AmmoUse 1;
		Weapon.AmmoGive 20;
		Weapon.AmmoType "Clip";
		Inventory.PickupMessage "$GOTCHAINGUN";
		Obituary "$OB_MPCHAINGUN";
		Tag "$TAG_CHAINGUN";
		Decal "BulletChip";
	}
	States
	{
	Ready:
		CHGN A 1 A_WeaponReady;
		Loop;
	Deselect:
		CHGN A 1 A_DeselectionShit();
		"####" A 1 A_WeaponOffset(0,33,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,35,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,39,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,44,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,49,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,55,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,60,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,70,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,80,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,90,WOF_INTERPOLATE);
	FinishDeselect:
		"####" A 0 A_Lower;
		Loop;
	Select:
		CHGN A 1 A_WeaponOffset(0,90,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,80,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,70,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,60,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,55,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,49,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,44,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,39,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,35,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,33,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,32,WOF_INTERPOLATE);
		"####" A 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE);
		goto Ready;
	Fire:
		CHGN CD 3 BRIGHT
		{
			A_PlaySound("weapons/chaingun/fire", CHAN_WEAPON);
			A_GunFlash();
			A_FireBullets(5.6, 0, 1, 5, "BulletPuff");
			A_ZoomFactor(frandom(0.996,0.986),ZOOM_INSTANT|ZOOM_NOSCALETURNING);
			A_WeaponOffset(random(-3,3),random(30,35),WOF_INTERPOLATE);
			A_QuadNoise();
		}
		CHGN C 0 A_ReFire;
		Goto SpinDown;
	SpinDown:
		CHGN B 3
		{
			A_ZoomFactor(1.0,ZOOM_NOSCALETURNING);
			A_WeaponOffset(0,32,WOF_INTERPOLATE);
			A_PlaySound("weapons/chaingun/wind",CHAN_5);
		}
		CHGN AB 3;
		CHGN A 5;
		CHGN B 8;
		goto Ready;
	Flash:
		TNT1 A 4 Bright A_Light1;
		Goto LightDone;
		TNT1 B 4 Bright A_Light2;
		Goto LightDone;
	Spawn:
		CHGN Z -1;
		Stop;
	}
}