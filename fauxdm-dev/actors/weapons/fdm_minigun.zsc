/*
================================================================================
 FauxDM ZScript Code
 
 Author: The Kinsie, edited from Realm667 Armory "UberMinigun" by lolo_is_cool
 
 Purpose: Minigun weapon (fills Skulltag Minigun DoomEdNum)
 
 Comment: Ported over from Reelism!

================================================================================
*/


class FDM_Minigun : FDM_BaseWeapon
{
	Default
	{
		Inventory.PickupMessage "$GOTMINIGUN";
		Tag "$TAGMINIGUN";
		Inventory.PickupSound "misc/w_pkup";
		Weapon.SelectionOrder 400;
		Weapon.Kickback 90;
		Weapon.AmmoType "Clip";
		Weapon.AmmoGive 100;
		Weapon.AmmoUse 1;
		Decal "BulletChip";
		//$Category "Weapons"
		//$Title "Minigun"
		//$NotAngled
		//$Color 14
	}
	States
	{
	Spawn:
		TSSX A -1;
		loop;
	Ready:
		TSSN A 1 A_WeaponReady;
		Loop;
	Deselect:
		TSSN A 1 A_DeselectionShit();
		"####" A 1 A_WeaponOffset(0,33,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,35,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,39,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,44,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,49,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,55,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,60,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,70,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,80,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,90,WOF_INTERPOLATE);
	FinishDeselect:
		"####" A 0 A_Lower;
		Loop;
	Select:
		TSSN A 1 A_WeaponOffset(0,90,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,80,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,70,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,60,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,55,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,49,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,44,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,39,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,35,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,33,WOF_INTERPOLATE);
		"####" A 1 A_WeaponOffset(0,32,WOF_INTERPOLATE);
		"####" A 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE);
		goto Ready;
	Fire:
		TSSN B 6 A_PlaySound("weapons/minigunup",6);
		TSSN C 5;
		TSSN D 4;
		TSSN A 3;
		TSSN B 2;
		TSSN CDABCDABCD 1;
		TSSN D 0 A_ReFire;
	WindDown:
		TSSN A 1 A_PlaySound("weapons/minigundown",6);
		TSSN B 2;
		TSSN C 3;
		TSSN D 4;
		TSSN A 5;
		TSSN BCD 6;
		TNT1 A 0 A_CheckReload;
		Goto Ready;
	Hold:
		TSSF A 0 
		{
			A_GunFlash();
			A_Quake(8,2,0,128,"null");
			A_PlaySound("weapons/minigun",CHAN_WEAPON);
			A_PlaySound("weapons/minigunhold",6,1,1);
		}
		TSSF B 1 BRIGHT A_FireBullets(5.6,0,2,5,"BulletPuff");
		TSSN D 1; //A_FireCustomMissile("BulletCasingSpawn",5,0,8,-9);
		TSSN D 0 A_JumpIfNoAmmo("WindDown");
		TSSN D 0 A_ReFire;
		Goto WindDown;
	}
}