/*
================================================================================
 FauxDM ZScript Code
 
 Author: The Kinsie
 
 Purpose: Visual effects
 
 Comment: 

================================================================================
*/

class FDM_ActorSpawnFog : Actor
{
	default
	{
		+NOBLOCKMAP
		+NOTELEPORT
		+NOGRAVITY
		+ZDOOMTRANS
		RenderStyle "Add";
	}
	States
	{
	Spawn:
		TFOG A 0;
		TFOG AAAAA 0 A_SpawnItemEx("FDM_ActorSpawnFogParticle",0,0,0,random(-5,5),random(-5,5),random(-5,5),random(0,360),SXF_NOCHECKPOSITION);
		TFOG A 0 A_PlaySound ("actors/spawn", CHAN_BODY);
		TFOG ABCDEFGHIJ 1 Bright;
		Stop;
	}	
}

class FDM_ActorSpawnFogParticle : Actor
{
	Default
	{
		+NOBLOCKMAP
		+NOTELEPORT
		+NOGRAVITY
		-SOLID
		RenderStyle "Add";
		speed 5;
		scale 0.5;
		radius 1;
		height 1;
	}

	States
	{
	Spawn:
		TFOG ABCDEFGHIJ 1 Bright;
		Stop;
	}
}

// Item Respawn Flash
class FDM_ItemFog : Actor replaces ItemFog
{
	default
	{
		+NOBLOCKMAP
		+NOGRAVITY
	}
	States
	{
	Spawn:
		IFOG A 0;
		IFOG AAAAA 0 A_SpawnItemEx("FDM_ItemFogParticle",0,0,0,random(-5,5),random(-5,5),random(-5,5),random(0,360),SXF_NOCHECKPOSITION);
		IFOG ABABCDE 1 BRIGHT;
		Stop;
	}
}

class FDM_ItemFogParticle : Actor
{
	Default
	{
		+NOBLOCKMAP
		+NOTELEPORT
		+NOGRAVITY
		-SOLID
		RenderStyle "Add";
		speed 5;
		scale 0.5;
		radius 1;
		height 1;
	}

	States
	{
	Spawn:
		IFOG ABCDEFGHIJ 1 Bright;
		Stop;
	}
}

// Water Splash - recycled from NULL
class FDM_WaterSplash : Actor
{
	default
	{
		Radius 2;
		Height 4;
		Gravity 0.125;
		+NOBLOCKMAP 
		+MISSILE 
		+DROPOFF
		+NOTELEPORT
		+CANNOTPUSH
		+DONTSPLASH
		+DONTBLAST
		renderstyle "Translucent";
		alpha 0.75;
	}
	States
	{
	Spawn:
		SPSH ABC 8
		{
			for (int i; i < 3; i++)
			{
				A_SpawnParticle("506094",SPF_RELATIVE,3,0,0,0,-8,(height / 2),frandom(2.0,-2.0),frandom(2.0,-2.0),frandom(1,8),0,0,-0.4);
				A_SpawnParticle("White",SPF_RELATIVE,3,0,0,-8,0,(height / 2),frandom(2.0,-2.0),frandom(2.0,-2.0),frandom(1,8),0,0,-0.4);
			}
		}
		SPSH DDDD 4
		{
			for (int i; i < 3; i++)
			{
				A_SpawnParticle("506094",SPF_RELATIVE,3,0,0,0,-8,(height / 2),frandom(2.0,-2.0),frandom(2.0,-2.0),frandom(1,8),0,0,-0.4);
				A_SpawnParticle("White",SPF_RELATIVE,3,0,0,-8,0,(height / 2),frandom(2.0,-2.0),frandom(2.0,-2.0),frandom(1,8),0,0,-0.4);
			}
		}
		Stop;
	Death:
		SPSH DDDD 4;
		Stop;
	}
}


class FDM_WaterSplashBase : Actor
{
	default
	{
		+NOBLOCKMAP
		+NOCLIP
		+NOGRAVITY
		+DONTSPLASH
		+DONTBLAST
		renderstyle "Translucent";
		alpha 0.75;
	}
	States
	{
	Spawn:
		SPSH EF 3
		{
			for (int i; i < 8; i++)
			{
				A_SpawnParticle("506094",SPF_RELATIVE,random(0,64),random(3,8),0,0,-8,(height / 2),frandom(2.0,-2.0),frandom(2.0,-2.0),frandom(1,8),0,0,-0.4);
				A_SpawnParticle("White",SPF_RELATIVE,random(0,32),random(3,8),0,-8,0,(height / 2),frandom(2.0,-2.0),frandom(2.0,-2.0),frandom(1,8),0,0,-0.4);
			}
		}
		SPSH GHI 3;
		Stop;
	}
}

class FDM_WaterSplashBaseSmall : FDM_WaterSplashBase
{
	default
	{
		scale 0.5;
	}
	States
	{
	Spawn:
		SPSH EF 3
		{
			for (int i; i < 7; i++)
			{
				A_SpawnParticle("506094",SPF_RELATIVE,random(0,32),random(3,8),0,0,-8,(height / 2),frandom(2.0,-2.0),frandom(2.0,-2.0),frandom(1,8),0,0,-0.4);
				A_SpawnParticle("White",SPF_RELATIVE,random(0,16),random(3,8),0,-8,0,(height / 2),frandom(2.0,-2.0),frandom(2.0,-2.0),frandom(1,8),0,0,-0.4);
			}
		}
		SPSH GHI 3;
		Stop;
	}
}

// THE CARNAGE ACTORS
// Used some Samsara code as an extremely loose base
class FDM_Gib : Actor
{
    Default
	{
		Radius 1;
		Height 1;
		Speed 5;
		Projectile;
		-NOGRAVITY
		-SOLID
		+MOVEWITHSECTOR
		+CLIENTSIDEONLY
		+NOBLOCKMAP
		+NOTELEPORT
		Gravity 0.65;
		scale 0.5;
	}
	
	States
	{
	Death:
		GIBS A 0 A_Gravity;
		GIBS ABCDEFG 3;
		stop;
	}
	
	override void PostBeginPlay()
	{		
		self.vel.z = random(5,9);
	}
	
	override void Tick()
	{
		if (curstate == SpawnState)
		{
			for (int i; i < 3; i++)
			{
				A_SpawnParticle("Red",SPF_RELATIVE,random(0,56),random(3,8),0,0,-8,(height / 2),frandom(2.0,-2.0),frandom(2.0,-2.0),frandom(1,8),0,0,-0.4);
			}
		}
		
		Super.Tick();
	}
}

class FDM_Gib_Eye : FDM_Gib
{
	States
	{
	Spawn:
		GEYE ABCD 5;
		loop;
	Death:
		GEYE E 0 A_Gravity;
		GEYE EFGHIJ 3;
		stop;
	}
}

class FDM_Gib_Head : FDM_Gib
{
	States
	{
	Spawn:
		GHED ABCD 5;
		loop;
	Death:
		GHED E 0 A_Gravity;
		GHED EFGHIJKL 3;
		stop;
	}
}

class FDM_Gib_Heart : FDM_Gib
{
	States
	{
	Spawn:
		GHRT ABCD 5;
		loop;
	}
}

class FDM_Gib_Intestines : FDM_Gib
{
	States
	{
	Spawn:
		GINS ABCD 5;
		loop;
	}
}

class FDM_Gib_Ribs : FDM_Gib
{
	States
	{
	Spawn:
		GRIB ABCD 5;
		loop;
	}
}

class FDM_Gib_Spine : FDM_Gib
{
	States
	{
	Spawn:
		GSPN ABCD 5;
		loop;
	}
}

// Explosions
class FDM_ExplosiveBarrelExplosionVisualOnly : Actor
{
	Default
	{
	+NOCLIP
	+FORCEXYBILLBOARD 
	Projectile;
	Speed 0;
	RenderStyle "Add";
	}
	
	States
	{
	Spawn:
		TNT1 A 0;
		TNT1 A 0 
		{
			A_Quake (1,24,0,750,0);
			A_playSound("weapons/explode",CHAN_WEAPON);
		}
		BEXP ABCDEFGHIJKLM 2 Bright;
		Stop;
	}
}

class FDM_RocketExplosionVisualOnly : Actor
{
	Default
	{
	+NOCLIP
	+FORCEXYBILLBOARD 
	Projectile;
	Speed 0;
	RenderStyle "Add";
	scale 0.75;
	}
	
	States
	{
	Spawn:
		TNT1 A 0;
		TNT1 A 0 
		{
			A_Quake (1,24,0,750,0);
			A_playSound("weapons/explode",CHAN_WEAPON);
		}
		BEXP ABCDEFGHIJKLM 2 Bright;
		Stop;
	}
}