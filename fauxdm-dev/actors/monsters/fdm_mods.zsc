/*
================================================================================
 FauxDM ZScript Code
 
 Author: The Kinsie
 
 Purpose: Monsters from older mods
 
 Comment: 

================================================================================
*/

// Zombieman
class FDM_JPFGuy : FDM_BaseMonster
{
	Default
	{
		Health 100;
		Radius 16;
		Height 56;
		PainChance 200;
		Monster;
		+FLOORCLIP
		speed 4;
		FDM_BaseMonster.TauntSound "actors/jpfguy/taunt";
		PainSound "actors/jpfguy/pain";
		DeathSound "actors/jpfguy/die";
		Obituary "$OB_JPFGUY";
		DropItem "Clip";
		Decal "BulletChip";
		translation "112:127=[48,165,91]:[9,23,14]";
	}
	States
	{
	Spawn:
		JPFG Z 10 A_Look;
		goto Roam;
	Roam:
		JPFG AABBCCDD 4 A_FDMRoam();
		Loop;
	See:
		JPFG AABBCCDD 4 A_FDMChase;
		Loop;
	Missile:
		JPFG E 0
		{
			bNOTAUTOAIMED = 1;
			height = 28;
		}
		JPFG E 8 A_FaceTarget;
		JPFG F 2
		{
			A_PlaySound ("actors/jpfguy/fire");
			A_CustomBulletAttack (5.6, 5.6, 1, random(6,12), "BulletPuff", 0, CBAF_NORANDOM);
		}
		JPFG E 2 A_FaceTarget;
		JPFG F 2
		{
			A_PlaySound ("actors/jpfguy/fire");
			A_CustomBulletAttack (5.6, 5.6, 1, random(6,12), "BulletPuff", 0, CBAF_NORANDOM);
		}
		JPFG E 2 A_FaceTarget;
		JPFG F 2
		{
			A_PlaySound ("actors/jpfguy/fire");
			A_CustomBulletAttack (5.6, 5.6, 1, random(6,12), "BulletPuff", 0, CBAF_NORANDOM);
		}
		JPFG E 8 A_FaceTarget;
		JPFG Y 4;
		JPFG Z 4 
		{
			bNOTAUTOAIMED = 0;
			height = 56;
		}
		Goto See;
	Pain:
		JPFG G 0
		{
			bNOTAUTOAIMED = 0;
			height = 56;
		}
		JPFG G 3;
		JPFG G 3 A_Pain;
		Goto See;
	Death:
		JPFG H 5;
		JPFG I 5 A_Scream;
		JPFG J 5 A_NoBlocking;
		JPFG K 5;
		JPFG L -1;
		Stop;
	}
	
	override void PostBeginPlay ()
	{
		Super.PostBeginPlay ();
		bFRIGHTENED = random(0,1);
	}
}

// Cyber-Bruiser
class FDM_CyberBruiser : FDM_BaseMonster
{
	Default
	{
		Health 200;
		Radius 16;
		Height 56;
		PainChance 200;
		Monster;
		+FLOORCLIP
		speed 3;
		FDM_BaseMonster.TauntSound "actors/cyberbruiser/taunt";
		PainSound "actors/cyberbruiser/pain";
		DeathSound "actors/cyberbruiser/die";
		Obituary "$OB_CYBERBRUISER";
		scale 0.85;
	}
	States
	{
	Spawn:
		CBOS A 10 A_Look;
		goto Roam;
	Roam:
		CBOS AABBCCDD 4 A_FDMRoam();
		Loop;
	See:
		CBOS AABBCCDD 4 A_FDMChase;
		Loop;
	Missile:
		CBOS E 0 A_PlaySound("actors/cyberbruiser/arm", CHAN_WEAPON);
		CBOS E 12 A_FaceTarget;
		CBOS F 8 BRIGHT 
		{
			A_PlaySound("weapons/rocket/fire", CHAN_WEAPON);
			A_SpawnProjectile("FDM_Rocket",27,13,0);
		}
		CBOS E 12;
		Goto See;
	Pain:
		CBOS H 3;
		CBOS H 3 A_Pain;
		Goto See;
	Death:
		CBOS I 8;
		CBOS J 8 A_Scream;
		CBOS K 8 A_NoBlocking;
		CBOS L 8;
		CBOS M -1;
		Stop;
	}
	
	// Give the actor a real stupid name.
	override void GiveStupidName()
	{
		static const string NameList[] =
		{
			// Less Dumb Stuff
			"Cyber-Baron",
			"Cyber-Hell Knight",
			"Cyber-Bruiser",
			"Terminator",
			"Cyber-DoorWithHealth",
			// Real Dumb Stuff
			"LittleBabbyBabbyMan",
			"CyberdemonDiaperfur",
			"AngerGremlin",
			"HOOONK",
			"Cyber-Buns",
			"H*ckBoy(tm)",
			"RokkitRidah",
			// Just Goat Stuff
			"GoatSimulator",
			"Goatse",
			"PinkPhillip",
			"Billy"
		};
		int i;
		i = random(0,NameList.Size()-1);
		SetTag(NameList[i]);
	}
	
	override void PostBeginPlay ()
	{
		Super.PostBeginPlay ();
		bFRIGHTENED = random(0,1);
	}
}