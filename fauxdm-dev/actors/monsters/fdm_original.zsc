/*
================================================================================
 FauxDM ZScript Code
 
 Author: The Kinsie
 
 Purpose: New and interesting monsters
 
 Comment: 

================================================================================
*/

// Pacifist! Runs around trying to steal all the items so nobody can use them to
// fight, ala Perfect Dark's PacifistSims. Drops any collected weapons on death.
// Uses the player sprites from the Jan 15 1995 Shadow Warrior prototype.
class FDM_Pacifist : FDM_BaseMonster
{
	Default
	{
		Health 201; // gross hack, see Tick()
		Radius 16;
		Height 56;
		PainChance 200;
		Monster;
		+FLOORCLIP
		+PICKUP
		+AMBUSH
		speed 4;
		FDM_BaseMonster.TauntSound "actors/pacifist/taunt";
		PainSound "actors/pacifist/pain";
		DeathSound "actors/pacifist/die";
		ActiveSound "grunt/active";
		Obituary "$OB_PACIFIST";
		yscale 0.9;
	}
	
	int targettimer;
	
	States
	{
	Spawn:
		PACI Z 10 A_Look;
		goto Roam;
	Roam:
		PACI ABCDEF 4 A_FDMRoamNoLook();
		Loop;
	See:
		PACI ABCDEF 4 A_FDMChase(null,null);
		PACI A 0 A_FaceTarget();
		Loop;
	Pain:
		PACI G 3;
		PACI G 3 A_Pain;
		Goto See;
	Death:
		PACI H 5;
		PACI I 5 
		{
			A_Scream();
			A_DropWeapons();
		}
		PACI J 5 A_NoBlocking;
		PACI K -1;
		Stop;
	GiveUp:
		PACI YX 12;
		loop;
	XDeath:
		GUTS A 0 A_DropWeapons();
		goto Super::XDeath;
	}
	
	override void Tick()
	{
		targettimer++;
		
		// A dumb hack so that the monster always picks up health items regardless of need
		if (health >= 201)
		{
			health = 200;
		}
		
		if (targettimer > 35 && target == null)
		{
			FindAnItem();
		}
		
		if (targettimer > 96)
		{
			target = null;
			goal = null;
			targettimer = 0;
		}
		
		// Don't target players or respawning items, you goof!
		if ((target is "PlayerPawn" || target is "FDM_BaseMonster") && (target != null && target.bInvisible == false))
		{
			target = null;
			goal = null;
			targettimer = 0;
		}
		
		Super.Tick();
	}
	
	void FindAnItem()
	{
		// Target and set goal at some random inventory item that's hopefully attainable
		if ((!(target is "Inventory") || !(target is "Weapon")))
		{
			BlockThingsIterator it = BlockThingsIterator.CreateFromPos(pos.x,pos.y,pos.z,0,128,0);
			while (it.Next())
			{
				Actor mo = it.thing;
				
				if ((mo is "Inventory" || mo is "Weapon") && (!(mo is "FDM_HealthBonus") || !(mo is "FDM_ArmorBonus")) && mo.bInvisible == false)
				{
					target = mo;
					goal = mo;
					targettimer = 0;
					break;
				}
			}
		}
	}
	
	// Blank out target and goals when collecting an item, just in case it causes camping item spawn points
	override bool CanTouchItem(Inventory item)
	{
		target = null;
		goal = null;
		targettimer = 0;
		A_PlaySound(TauntSound, CHAN_VOICE);
		return true;
	}
	
	// More or less disable alliance flickering
	override void DoAllianceFlicker()
	{
		bFRIENDLY = 1;
	}
	
	// We don't care about other players, so ignore them as best we can.
	void A_FDMRoamNoLook()
	{
		DoAvoidWallrunning();
		
		A_Wander();
		A_Recoil(-speed);
		oldPos = pos;
	}
	
	// Drop any collected weapons upon death
	void A_DropWeapons()
	{
		if (CheckInventory("FDM_Machinegun", 1))
		{
			A_DropItem ("FDM_Machinegun", -1, 256);
		}

		if (CheckInventory("FDM_Shotgun", 1))
		{
			A_DropItem ("FDM_Shotgun", -1, 256);
		}

		if (CheckInventory("FDM_SuperShotgun", 1))
		{
			A_DropItem ("FDM_SuperShotgun", -1, 256);
		}

		if (CheckInventory("FDM_Chaingun", 1))
		{
			A_DropItem ("FDM_Chaingun", -1, 256);
		}

		if (CheckInventory("FDM_Minigun", 1))
		{
			A_DropItem ("FDM_Minigun", -1, 256);
		}

		if (CheckInventory("FDM_RocketLauncher", 1))
		{
			A_DropItem ("FDM_RocketLauncher", -1, 256);
		}

		if (CheckInventory("FDM_GrenadeLauncher", 1))
		{
			A_DropItem ("FDM_GrenadeLauncher", -1, 256);
		}

		if (CheckInventory("FDM_PlasmaRifle", 1))
		{
			A_DropItem ("FDM_PlasmaRifle", -1, 256);
		}

		if (CheckInventory("FDM_Railgun", 1))
		{
			A_DropItem ("FDM_Railgun", -1, 256);
		}

		if (CheckInventory("FDM_BFG9000", 1))
		{
			A_DropItem ("FDM_BFG9000", -1, 256);
		}

		if (CheckInventory("FDM_Flamethrower", 1))
		{
			A_DropItem ("FDM_Flamethrower", -1, 256);
		}
	}
}